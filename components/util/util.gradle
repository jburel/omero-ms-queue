apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'jacoco'


dependencies {
    // only needed for util.servlet and util.spring.http:
    compile 'javax.servlet:javax.servlet-api:3.1.0'
    // only needed for util.spring.http:
    compile 'org.springframework:spring-web:4.3.9.RELEASE'
    // only needed for util.serialization.yaml.YamlConverter:
    compile 'org.yaml:snakeyaml:1.17'
    // only needed for util.serialization.json
    compile 'com.google.code.gson:gson:2.8.1'

    testCompile libset.test_base
}

jar {
    baseName = 'ome-util'
}

test {  // added by java plugin, but says 'Runs the unit tests.'
    description 'Runs all available tests.'

/* TODO temp workaround to Travis issues.
 * Currently we get these test failures on Travis:
 *
 *   integration.runtime.CommandRunnerTest > createOutputFileIfItDoesntExist FAILED
 *     java.lang.AssertionError at CommandRunnerTest.java:72
 *   integration.runtime.CommandRunnerTest > canReadOutputFile FAILED
 *     java.lang.AssertionError at CommandRunnerTest.java:72
 *   integration.runtime.CommandRunnerTest > appendToOutputFileIfItExists FAILED
 *     java.lang.AssertionError at CommandRunnerTest.java:104
 *   integration.runtime.CommandRunnerTest > canReadAllStreamedOutput FAILED
 *     java.lang.AssertionError at CommandRunnerTest.java:72
 *   integration.runtime.jvm.JvmLaunchTest > fullyFledgedMainClassLaunch FAILED
 *     java.lang.AssertionError at JvmLaunchTest.java:102
 *
 * But all of them used to pass on Travis, so we need to find out what
 * changes the Travis guys have introduced that make them fail now...
 */
    if (System.properties['test.profile'] == 'travis') {
        exclude '**/integration/runtime/*'
    }
}

task test1(type: Test) {
    group 'verification'
    description 'Runs the unit tests only.'
    exclude '**/integration/**'
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled true
    }
}
